'declare variables
dir = Program.Directory
appver = "1.0"
appnom = "The Block Game"
apptit = Text.Append(appnom + " ",appver)
cpos = ""
map = ""
lbut = ""
worldp = dir + "\world.wld"
world = ""
gui = ""
cgui = 0
ingame = "False"
blocktypes = ""
gwlkey = ""
saveworld = "False"
playerrenderdata = ""
renderreg = ""
renderobjreg = ""
imglstreg = ""

'this array has the next render data "x,y" per item
nextrenderarr = ""

'program
init()
runtime()

Program.End()
'screen is 53 by 30 blocks
Sub init
  'window init
  GraphicsWindow.Show()
  GraphicsWindow.Title = apptit
  GraphicsWindow.CanResize = "False"
  GraphicsWindow.Width = 850
  GraphicsWindow.Height = 480
  GraphicsWindow.BackgroundColor = "Gray"
  'init font
  GraphicsWindow.FontName = "Consolas"
  GraphicsWindow.FontSize = 72
  GraphicsWindow.FontBold = "True"
  'define splash text
  GraphicsWindow.PenColor = "Red"
  GraphicsWindow.BrushColor = "Red"
  sptxt = Shapes.AddText("Captain ALM")
  Shapes.Move(sptxt,850/5,480/3)
  Program.Delay(500)
  initgr()
  Program.Delay(500)
  initrunner()
  Shapes.HideShape(sptxt)
EndSub

Sub initgr
  'placement property: 0 = background 1 = inline 2 = foreground
  blocktypes["air"]["placement"] = 0
  'image property: the value is the image location or img:(colour) for a coloured block
  blocktypes["air"]["image"] = "img:#ffffff"
  'gravity property: 0 = no gravity 1 = gravity
  blocktypes["air"]["gravity"] = 1
  
  'register test blocks
  blocktypes["block"]["placement"] = 1
  blocktypes["block"]["image"] = "img:#000000"
  blocktypes["win"]["placement"] = 0
  blocktypes["win"]["image"] = "img:#1000ff"
  blocktypes["win"]["gravity"] = 1
  blocktypes["win"]["action"]["enter"] = "output:Completed Level,regenarate"
  blocktypes["die"]["placement"] = 2
  blocktypes["die"]["image"] = "img:#ff0000"
  blocktypes["die"]["gravity"] = 0
  blocktypes["win"]["action"]["enter"] = "output:Failed Level,position:0*0"
  
  'register player renderer: the value is the image location or img:(colour) for a coloured block
  playerrenderdata = "img:#095de5"
  
  'load all image data
  If Text.StartsWith(playerrenderdata,"img:") <> "True" Then
    imglstreg[playerrenderdata] = ImageList.LoadImage(playerrenderdata)
  EndIf
  bttmp = Array.GetAllIndices(blocktypes)
  For i_img_ll = 1 To Array.GetItemCount(blocktypes)
    If Text.StartsWith(blocktypes[bttmp[i_img_ll]]["image"],"img:") <> "True" Then
      blocktypes[bttmp[i_img_ll]]["image"] = ImageList.LoadImage(blocktypes[bttmp[i_img_ll]]["image"])
    EndIf
  EndFor
EndSub

Sub initrunner
  GraphicsWindow.FontSize = 48
  GraphicsWindow.PenColor = "Red"
  GraphicsWindow.BrushColor = "Red"
  gui[1][1] = Shapes.AddText("The Block Game")
  Shapes.Move(gui[1][1],50,20)
  GraphicsWindow.FontSize = 24
  GraphicsWindow.PenColor = "Black"
  GraphicsWindow.BrushColor = "Black"
  gui[1][2] = Controls.AddButton("Play Game",50,135)
  gui[1][3] = Controls.AddButton("Reset Game",50,235)
  gui[1][4] = Controls.AddButton("About Game",50,335)
  gui[1][5] = Controls.AddButton("Quit Game",50,435)
  cgui = 1
  hidegui()
  cgui = 0
  
  initgame()
  cgui = 2
  hidegui()
  cgui = 0
  
  GraphicsWindow.PenColor = "Black"
  GraphicsWindow.BrushColor = "Gray"
  gui[3][1] = Shapes.AddRectangle(GraphicsWindow.Width,GraphicsWindow.Height)
  Shapes.Move(gui[3][1],0,0)
  Shapes.SetOpacity(gui[3][1],75)
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.FontSize = 24
  gui[3][2] = Shapes.AddText("Game Paused")
  Shapes.Move(gui[3][2],850/5,40)
  gui[3][3] = Controls.AddButton("Return to Menu",850/5,200)
  gui[3][4] = Controls.AddButton("Save and Return to Menu",850/5,280)
  gui[3][5] = Controls.AddButton("Close Game",850/5,360)
  gui[3][6] = Controls.AddButton("Return to Game",850/5,120)
  cgui = 3
  hidegui()
  cgui = 0
  
  Controls.ButtonClicked = onbut
  GraphicsWindow.KeyDown = onkeydown
EndSub

Sub runtime
  cgui = 1
  showgui()
  
  While "True"
    If butd Then
      If lbut = gui[1][2] then
        lbut = ""
        ingame = "True"
        hidegui()
      ElseIf lbut = gui[1][3] then
        lbut = ""
        File.DeleteFile(worldp)
        Sound.PlayChime()
      ElseIf lbut = gui[1][4] then
        lbut = ""
        Sound.PlayChime()
        GraphicsWindow.ShowMessage("About The Block Game - By Captain ALM", apptit)
      ElseIf lbut = gui[1][5] then
        lbut = ""
        Program.End()
      EndIf
      butd = "False"
    EndIf
    If ingame Then
      cgui = 2
      Shapes.SetText(sptxt,"Loading Game...")
      Shapes.ShowShape(sptxt)
      world = File.ReadContents(worldp)
      cpos = ""
      map = ""
      If world = "" then
        cpos["x"] = 1
        cpos["y"] = 1
        world["pos"] = cpos
        generate()
        world["map"] = map
      Else
        cpos = world["pos"]
        map = world["map"]
      EndIf
      Shapes.HideShape(sptxt)
      showgui()
      renderall()
      While ingame
        gametick()
        Program.Delay(10)
      EndWhile
      If saveworld then
        saveworld = "False"
        Shapes.SetText(sptxt,"Saving Game...")
        Shapes.ShowShape(sptxt)
        world["pos"] = cpos
        world["map"] = map
        File.WriteContents(worldp,world)
        Shapes.HideShape(sptxt)
      EndIf
      world = ""
      cpos = ""
      map = ""
      hidegui()
      cgui = 1
      showgui()
    EndIf
    Program.Delay(10)
  EndWhile
EndSub

Sub initgame
  
EndSub

Sub generate
  'Layers:
  'Air layer is 1-15
  'Blocked layer is 16-25
  'Death layer is 26-30
  For i_gen = 1 To 30 Step 1
    If i_gen < 16 Then
      For i_gen_i = 1 To 53 Step 1
        map[i_gen_i][i_gen] = "air"
      EndFor
    EndIf
    If i_gen < 26 And i_gen > 15 Then
      For i_gen_i = 1 To 53 Step 1
        map[i_gen_i][i_gen] = "block"
      EndFor
    EndIf
    If i_gen < 31 And i_gen > 25 Then
      For i_gen_i = 1 To 53 Step 1
        map[i_gen_i][i_gen] = "die"
      EndFor
    EndIf
  EndFor
EndSub

Sub render
  If nextrenderarr <> "" Then
    For i_odren = 1 To Array.GetItemCount(nextrenderarr)
      i_render_x = Text.GetSubText(nextrenderarr[i_odren],1,Text.GetIndexOf(nextrenderarr[i_odren],",") - 1)
      i_render_y = Text.GetSubTextToEnd(nextrenderarr[i_odren],Text.GetIndexOf(nextrenderarr[i_odren],",") + 1)
      If i_render_x = cpos["x"] And i_render_y = cpos["y"] Then
        renderreg[i_render_x][i_render_y] = playerrenderdata
      Else
        renderreg[i_render_x][i_render_y] = blocktypes[map[i_render_x][i_render_y]]["image"]
      Endif
      If Text.StartsWith(renderreg[i_render_x][i_render_y],"img:") Then
        obc = GraphicsWindow.BrushColor
        opc = GraphicsWindow.PenColor
        GraphicsWindow.BrushColor = Text.GetSubTextToEnd(renderreg[i_render_x][i_render_y],5)
        GraphicsWindow.PenColor = Text.GetSubTextToEnd(renderreg[i_render_x][i_render_y],5)
        renderobjreg[i_render_x][i_render_y] = Shapes.AddRectangle(16,16)
        Shapes.Move(renderobjreg[i_render_x][i_render_y],(i_render_x-1)*16,(i_render_y-1)*16)
        GraphicsWindow.BrushColor = obc
        GraphicsWindow.PenColor = opc
      Else
        renderobjreg[i_render_x][i_render_y] = Shapes.AddImage(imglstreg[renderreg[i_render_x][i_render_y]])
        Shapes.Move(renderobjreg[i_render_x][i_render_y],(i_render_x-1)*16,(i_render_y-1)*16)
      EndIf
    EndFor
  EndIf
EndSub

Sub unrender
  If nextrenderarr <> "" Then
    For i_odren = 1 To Array.GetItemCount(nextrenderarr)
      i_render_x = Text.GetSubText(nextrenderarr[i_odren],1,Text.GetIndexOf(nextrenderarr[i_odren],",") - 1)
      i_render_y = Text.GetSubTextToEnd(nextrenderarr[i_odren],Text.GetIndexOf(nextrenderarr[i_odren],",") + 1)
      Shapes.Remove(renderobjreg[i_render_x][i_render_y])
    EndFor
  EndIf
EndSub

Sub renderall
  For i_render_x = 1 To 53 Step 1
    For i_render_y = 1 to 53 step 1
      If i_render_x = cpos["x"] And i_render_y = cpos["y"] Then
        renderreg[i_render_x][i_render_y] = playerrenderdata
      Else
        renderreg[i_render_x][i_render_y] = blocktypes[map[i_render_x][i_render_y]]["image"]
      Endif
      If Text.StartsWith(renderreg[i_render_x][i_render_y],"img:") Then
        obc = GraphicsWindow.BrushColor
        opc = GraphicsWindow.PenColor
        GraphicsWindow.BrushColor = Text.GetSubTextToEnd(renderreg[i_render_x][i_render_y],5)
        GraphicsWindow.PenColor = Text.GetSubTextToEnd(renderreg[i_render_x][i_render_y],5)
        renderobjreg[i_render_x][i_render_y] = Shapes.AddRectangle(16,16)
        Shapes.Move(renderobjreg[i_render_x][i_render_y],(i_render_x-1)*16,(i_render_y-1)*16)
        GraphicsWindow.BrushColor = obc
        GraphicsWindow.PenColor = opc
      Else
        renderobjreg[i_render_x][i_render_y] = Shapes.AddImage(imglstreg[renderreg[i_render_x][i_render_y]])
        Shapes.Move(renderobjreg[i_render_x][i_render_y],(i_render_x-1)*16,(i_render_y-1)*16)
      EndIf
    EndFor
  EndFor
EndSub

Sub unrenderall
  For i_render_x = 1 To 53 Step 1
    For i_render_y = 1 to 53 step 1
      Shapes.Remove(renderobjreg[i_render_x][i_render_y])
    EndFor
  EndFor
  renderobjreg = ""
  renderreg = ""
EndSub

Sub gametick
  'if player can fall due to gravity
  If blocktypes[map[cpos["x"]][cpos["y"]]]["gravity"] = 1 And blocktypes[map[cpos["x"]][cpos["y"]]]["placement"] <> 1 And cpos["y"] < 30 and blocktypes[map[cpos["x"]][cpos["y"]+1]]["placement"] <> 1 Then
    'rerender bits needed when player falls due to gravity #1
    nextrenderarr = ""
    nextrenderarr[1] = cpos["x"] + "," + cpos["y"]
    'make player fall
    cpos["y"] = cpos["y"] + 1
    'rerender bits needed when player falls due to gravity #2
    nextrenderarr[2] = cpos["x"] + "," + cpos["y"]
    unrender()
    render()
    nextrenderarr = ""
  EndIf
  If gwlkey = "Escape" Then
    unrenderall()
    gwlkey = ""
    hidegui()
    cgui = 3
    showgui()
    While cgui = 3
      If lbut = gui[3][3] Then
        lbut = ""
        hidegui()
        cgui = 1
        ingame = "False"
      ElseIf lbut = gui[3][4] then
        lbut = ""
        hidegui()
        cgui = 1
        ingame = "False"
        saveworld = "True"
      ElseIf lbut = gui[3][5] then
        lbut = ""
        Program.End()
      ElseIf lbut = gui[3][6] then
        lbut = ""
        hidegui()
        cgui = 2
        showgui()
        renderall()
      elseIf gwlkey = "Escape" Then
        gwlkey = ""
        hidegui()
        cgui = 2
        showgui()
        renderall()
      EndIf
      Program.Delay(10)
    EndWhile
  EndIf
EndSub

Sub hidegui
  For i = 1 To Array.GetItemCount(gui[cgui])
    Shapes.HideShape(gui[cgui][i])
  EndFor
EndSub

Sub showgui
  For i = 1 To Array.GetItemCount(gui[cgui])
    Shapes.ShowShape(gui[cgui][i])
  EndFor
EndSub

Sub onbut
  butd = "True"
  lbut = Controls.LastClickedButton
EndSub

Sub onkeydown
  gwlkey = GraphicsWindow.LastKey
EndSub